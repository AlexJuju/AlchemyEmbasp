#show place/5.
height(0..7).
width(0..8).

placed(I, J) :- placedRune(I, J, _, _, _).

adiacente(I1, J, I2, J) :- height(I1), width(J), height(I2), I2 = I1+1.
adiacente(I, J1, I, J2) :- height(I), width(J1), width(J2), J2 = J1+1.
adiacente(I1, J, I2, J) :- height(I1), width(J), height(I2), I2 = I1-1.
adiacente(I, J1, I, J2) :- height(I), width(J1), width(J2), J2 = J1-1.

place(I, J, S, C, T) | noPlace(I, J, S, C, T) :- height(I), width(J), rune(S, C, T), not placed(I, J).

%Esclude che la stessa runa sia in piu' posizioni.
:- #count{I, J : place(I,J,_,_,_)} > 1.

%Esclude due rune incompatibili vicine.
:- place(I1, J1, S1, C1, normal), placedRune(I2, J2, S2, C2, normal), adiacente(I1, J1, I2, J2), S1 != S2, C1 != C2.

%Esclude che una runa che si sta piazzando non abbia confinanti.
runevicine :- place(I1, J1, _, _, normal), placed(I2, J2), adiacente(I1, J1, I2, J2).
runevicine :- place(_, _, _, _, stone).
:- not runevicine.